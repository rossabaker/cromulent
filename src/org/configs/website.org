#+title: Experimenting with ox-publish
#+PROPERTY: header-args      :results silent
#+PROPERTY: header-args:nix  :eval no
#+LAST_MODIFIED: <2023-05-24 Wed 23:33>

This is an experiment.  It might replace ox-hugo.  It might not.  It's
not live yet.

* Nix build

The website is a package within our flake.

#+begin_src nix :tangle ../../../gen/website/default.nix
  { src, emacs29, gnupg, hugo, html5validator, stdenv }:

  let
    siteEmacs = emacs29.pkgs.withPackages (epkgs: [
      epkgs.dash
      epkgs.esxml
      epkgs.ox-hugo
    ]);
  in
  stdenv.mkDerivation rec {
    name = "rossabaker.com";
    inherit src;
    nativeBuildInputs = [
      siteEmacs
      gnupg
      hugo
      html5validator
    ];
    buildPhase = ''
      cd ..
      export PATH=${gnupg}/bin:$PATH
      # https://emacs.stackexchange.com/a/70847
      ${siteEmacs}/bin/emacs --batch -l ob -l ob-shell --eval "
	(let ((org-confirm-babel-evaluate nil))
	  (with-current-buffer (find-file-noselect \"src/org/configs/website.org\")
	    (org-babel-execute-buffer)
	    (save-buffer)))
      "
      ${hugo}/bin/hugo --config tmp/hugo/config.toml
    '';

    doCheck = true;
    checkPhase = ''
      html5validator --log INFO --root tmp/hugo/static
    '';

    installPhase = ''
      mkdir $out
      cp -r public/. $out
    '';
  }
#+end_src

** Try it locally

To build the site locally into ~./result~, run:

#+begin_src sh :tangle no
  nix build .#website
#+end_src

* Setup

#+begin_src emacs-lisp
  (require 'dash)
  (require 'ox)
  (require 'esxml)
#+end_src

* Template

** Base HTML template

#+begin_src emacs-lisp
  (defun rab/html-template (contents info)
    (let ((title (org-export-data (plist-get info :title) info)))
      (concat
       "<!DOCTYPE html>\n"
       (esxml-to-xml
	`(html ()
	  (head ()
	   (title () ,title))
	  (body ()
	   ,(rab/site-header)
	   (main () (raw-string ,contents))))))))
#+end_src

** Site header

#+begin_src emacs-lisp
  (defun rab/site-header ()
    `(header ()
      (strong () (a ((href . "/")) "rossabaker"))
      (p () "A perfectly cromulent developer.")
      (nav ()
       (ul ()
	,@(mapcar (lambda (entry)
		    (let ((href (car entry))
			  (body (cdr entry)))
		     `(li () (a ((href . ,href)) ,body))))
	   '(("/configs" . "Configs")))))))
#+end_src

* Backend

** Derived from HTML

We use the ox-html backend as a starting point, customizing individual
components as necessary.

One obvious one is =template=, which lets us use our esxml template.

#+begin_src emacs-lisp
  (org-export-define-derived-backend 'rab/html 'html
    :translate-alist
    '((template . rab/html-template)))
#+end_src

** Pretty URLs

We have to do some jiggery pokery to get "clean URLs" (e.g., make
~/configs/website.org~ render to ~/config/website/~).  We need to
locally redefine =org-export-output-file-name=.

#+begin_src emacs-lisp
  (defun rab/publish-to-html (plist filename pub-dir)
    (let ((output-dir (concat pub-dir
			      (->> filename
				   file-name-nondirectory
				   file-name-sans-extension
				   file-name-as-directory))))
      (cl-letf (((symbol-function 'org-export-output-file-name)
		 (lambda (extension &optional subtreep pub-dir)
		   (concat output-dir "index" extension))))
	(org-publish-org-to 'rab/html filename
			    (concat (when (> (length org-html-extension) 0) ".")
				    (or (plist-get plist :html-extension)
					org-html-extension
					"html"))
			    plist
			    output-dir))))
#+end_src

* Publish

** Legacy ox-hugo build

This is the =export.el= referred to in the Nix build.  It is temporary
while we unwind the ox-hugo setup.

#+begin_src emacs-lisp
  (require 'ox-hugo)
  (require 'ob-shell)
  (with-current-buffer (find-file-noselect "../rossabaker.org")
    (let ((org-confirm-babel-evaluate nil))
      (org-babel-tangle)
      (org-hugo-export-wim-to-md t)))
#+end_src

** ox-html setup

This will be the main build moving forward.

#+begin_src emacs-lisp
  (setq-local user-full-name "Ross A. Baker"
	      org-publish-timestamp-directory "../../../tmp/org-timestamps")

  (setq org-publish-project-alist
	`(("org"
	   :base-directory "../"
	   :recursive t
	   :exclude ,(rx (or (seq (or "config" "img" "talks" "tmp") "/" (* nonl)) "rossabaker.org"))
	   :publishing-function rab/publish-to-html
	   :publishing-directory "../../../tmp/hugo/static")))

  (org-publish-all)
#+end_src

# Local Variables:
# org-confirm-babel-evaluate: nil
# End:
