#+title: Experimenting with ox-publish

This is an experiment.  It might replace ox-hugo.  It might not.  It's
not live yet.

* Template

** Base HTML template

#+begin_src emacs-lisp
  (defun rab/html-template (contents info)
    (let ((title (org-export-data (plist-get info :title) info)))
      (concat
       "<!DOCTYPE html>\n"
       (esxml-to-xml
	`(html ()
	  (head ()
	   (title () ,title))
	  (body ()
	   ,(rab/site-header)
	   (main () (raw-string ,contents))))))))
#+end_src

#+RESULTS:
: rab/html-template

** Site header

#+begin_src emacs-lisp
  (defun rab/site-header ()
    `(header ()
      (strong () (a ((href . "/")) "rossabaker"))
      (p () "A perfectly cromulent developer.")
      (nav ()
       (ul ()
	,@(mapcar (lambda (entry)
		    (let ((href (car entry))
			  (body (cdr entry)))
		     `(li () (a ((href . ,href)) ,body))))
	   '(("/configs" . "Configs")))))))
#+end_src

#+RESULTS:
: rab/site-header

* Backend

#+begin_src emacs-lisp
  (org-export-define-derived-backend 'rab/html 'html
    :translate-alist
    '((template . rab/html-template)))

  (defun rab/publish-to-html (plist filename pub-dir)
    (org-publish-org-to 'rab/html filename
			(concat (when (> (length org-html-extension) 0) ".")
				(or (plist-get plist :html-extension)
				    org-html-extension
				    "html"))
			plist pub-dir))
#+end_src

#+RESULTS:
: rab/publish-to-html

* Publish

#+begin_src emacs-lisp
  (setq user-full-name "Ross A. Baker")

  (setq org-publish-project-alist
	`(("org"
	   :base-directory "../../"
	   :recursive t
	   :exclude ,(rx (or (seq (or "config" "img" "talks" "tmp") "/" (* nonl)) "rossabaker.org"))
	   :publishing-function rab/publish-to-html
	   :publishing-directory "../../../../tmp/public_html")))

  (org-publish-all)
#+end_src

#+RESULTS:

# Local Variables:
# org-confirm-babel-evaluate: nil
# End:
